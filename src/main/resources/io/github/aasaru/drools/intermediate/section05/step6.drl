package io.github.aasaru.drools.intermediate.section05.step6

import io.github.aasaru.drools.intermediate.domain.cep.Agent;
import io.github.aasaru.drools.intermediate.domain.cep.Call;
import io.github.aasaru.drools.intermediate.service.CallService;
import io.github.aasaru.drools.intermediate.service.AgentService;

import java.util.List;

global CallService callService;
global AgentService agentService;

rule "Connect an agent with a suitable call that has earliest start"
	dialect "java"
	when
		$agent: Agent()
		$callToPickUp: Call( $agent.speaks(this.language) )
		forall (
			$candidateCall : Call( $agent.speaks(this.language) ),
			Call( this == $candidateCall, this.callStart >= $callToPickUp.callStart )
		)
	then
		callService.connectWithAgent($callToPickUp, $agent);
		delete($agent);
		delete($callToPickUp);
end

rule "Nobody at work who speaks the language"
	dialect "java"
	when
		$callToHangUp: Call( )
		not( Agent( this.speaks($callToHangUp.language) ) from agentService.getAgentsAtWork() )
	then
		callService.dropCall($callToHangUp,
		"There are no agents currently at work who speak " + $callToHangUp.getLanguage());
		delete($callToHangUp);
end

rule "Output queue size"
	dialect "java"
	salience 100
	when
		$queue : List() from collect( Call() )
	then
		callService.reportQueueSize( $queue.size() );
end

// two rules reporting available agent stats are removed (to reduce noise in logs)

rule "Halt session if there are no calls"
	dialect "java"
	when
		not Call( )
	then
		callService.reportMessage("There have been no calls in queue. Halting.");
		drools.halt();
end
