package io.github.aasaru.drools.intermediate.section06.step5

import io.github.aasaru.drools.intermediate.section06.Agent;
import io.github.aasaru.drools.intermediate.section06.Call;
import io.github.aasaru.drools.intermediate.section06.CallService;
import io.github.aasaru.drools.intermediate.section06.AgentService;

import java.util.List;

global CallService callService;
global AgentService agentService;

rule "Connect an agent with a suitable call that has earliest start"
	dialect "java"
	when
		$agent: Agent( $agentLanguages : speaksLanguages )
		$agentLanguage: String() from $agentLanguages
		$callToPickUp: Call( this.language == $agentLanguage )
		forall (
			$suitableCall : Call( $agent.speaks(this.language) ),
			Call( this == $suitableCall, this.callStart >= $callToPickUp.callStart )
		)
	then
		callService.connectWithAgent($callToPickUp, $agent);
		delete($agent);
		delete($callToPickUp);
end

rule "Nobody at work who speaks the language"
	dialect "java"
	when
		$callToHangUp: Call( )
		not( Agent( this.speaks($callToHangUp.language) ) from agentService.getAgentsAtWork())
	then
		callService.dropCall($callToHangUp, "There are no agents currently at work who speak " + $callToHangUp.getLanguage());
		delete($callToHangUp);
end

rule "Output queue size"
	dialect "java"
	salience 100
	when
		$queue : List() from collect( Call() )
	then
		callService.reportQueueSize( $queue.size() );
end

rule "Available agents speaking French"
	dialect "java"
	salience 50
	when
		$frenchSpeakers : List() from collect( Agent( this.speaks("French") ) )
	then
		agentService.reportAgentStats("Available agents speaking French: " + $frenchSpeakers.size());
end

rule "All German speaking agents are currently available"
	dialect "java"
	when
		$germanSpeakersAvailable : List(size > 0) from collect( Agent( this.speaks("German") ) )
		$germanSpeakersAtWork : List(size == $germanSpeakersAvailable.size())
			from collect( Agent(this.speaks("German") )
				from agentService.getAgentsAtWork()
			)
	then
		agentService.reportAgentStats( "All "+ $germanSpeakersAtWork.size()+" German speaking agents are currently available.");
end
